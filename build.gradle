plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.1'
	id 'io.spring.dependency-management' version '1.1.0'
	id "com.google.protobuf" version "0.9.2"
}

group = 'com.namnh'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'io.grpc:grpc-protobuf:1.52.1'
	implementation 'io.grpc:grpc-stub:1.52.1'
	implementation 'com.google.protobuf:protobuf-java:3.21.12'
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.1'
	implementation 'org.springframework.hateoas:spring-hateoas:2.0.1'
	compileOnly 'org.projectlombok:lombok'
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	runtimeOnly 'com.h2database:h2:2.1.214'
	runtimeOnly 'io.grpc:grpc-netty-shaded:1.52.1'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.21.12"
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.52.1'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

task syncProto(type: Sync) {
	from 'build/generated/source/proto/main/grpc', 'build/generated/source/proto/main/java'
	into 'src/main/java'
	preserve {
		exclude '*'
	}
}

task cleanProto(type: Delete){
	delete 'build/generated/source/proto'
}

task generateProtoAndSync {
	dependsOn 'generateProto'
	dependsOn 'syncProto'
	dependsOn 'cleanProto'
	tasks.findByName('syncProto').mustRunAfter 'generateProto'
	tasks.findByName('cleanProto').mustRunAfter 'syncProto'
}
